<!DOCTYPE html>
<html dir="ltr" lang="en-gb">

<!-- Mirrored from www.dostips.com/forum/viewtopic.php?f=3&t=8546&view=print by HTTrack Website Copier/3.x [XR&CO'2014], Fri, 19 Oct 2018 07:21:48 GMT -->
<!-- Added by HTTrack --><meta http-equiv="content-type" content="text/html;charset=UTF-8" /><!-- /Added by HTTrack -->
<head>
<meta charset="utf-8" />
<meta http-equiv="X-UA-Compatible" content="IE=edge">
<meta name="robots" content="noindex" />

<title>DosTips.com &bull; Visualize a clock</title>

<link href="styles/AllanStyle-SUBSILVER/theme/print.css" rel="stylesheet">
</head>
<body id="phpbb">
<div id="wrap" class="wrap">
	<a id="top" class="top-anchor" accesskey="t"></a>

	<div id="page-header">
		<h1>DosTips.com</h1>
		<p>A Forum all about DOS Batch<br /><a href="index-2.html">https://www.dostips.com/forum/</a></p>

		<h2>Visualize a clock</h2>
		<p><a href="viewtopic9a53.html?f=3&amp;t=8546">https://www.dostips.com/forum/viewtopic.php?f=3&amp;t=8546</a></p>
	</div>

	<div id="page-body" class="page-body">
		<div class="page-number">Page <strong>1</strong> of <strong>1</strong></div>
					<div class="post">
				<h3>Visualize a clock</h3>
				<div class="date">Posted: <strong>06 May 2018 10:37</strong></div>
				<div class="author">by <strong>IcarusLives</strong></div>
				<div class="content">Hello all ^-^<br>
<br>
A great exercise is to visualize a clock in code. I suggest you all try it!<br>
<br>
Enjoy! WIN 10 ONLY! <br>
<br>
Excuse the flicker, I believe it is from GYAZO gif<br>
<img src="../../i.gyazo.com/52fc6c13f389f8d8c11f588833d5d239.gif" class="postimage" alt="Image">  <img src="../../i.gyazo.com/c9ed849947be13a62aacea7190035173.gif" class="postimage" alt="Image"><br>

<div class="codebox"><p>Code: <a href="#" onclick="selectCode(this); return false;">Select all</a></p><pre><code>@echo off &amp; setlocal EnableDelayedExpansion
mode 49,49

call :macros

    set /a "PI=(35500000/113+5)/10, PI_div_2=(35500000/113/2+5)/10, PIx2=2*PI, PI32=PI+PI_div_2"
    set "_SIN=a-a*a/1920*a/312500+a*a/1920*a/15625*a/15625*a/2560000-a*a/1875*a/15360*a/15625*a/15625*a/16000*a/44800000"
    set "SIN(x)=(a=(x * 31416 / 180)%%62832, c=(a&gt;&gt;31|1)*a, a-=(((c-47125)&gt;&gt;31)+1)*((a&gt;&gt;31|1)*62832)  +  (-((c-47125)&gt;&gt;31))*( (((c-15709)&gt;&gt;31)+1)*(-(a&gt;&gt;31|1)*31416+2*a)  ), %_SIN%) / 10000"
    set "COS(x)=(a=(15708 - x * 31416 / 180)%%62832, c=(a&gt;&gt;31|1)*a, a-=(((c-47125)&gt;&gt;31)+1)*((a&gt;&gt;31|1)*62832)  +  (-((c-47125)&gt;&gt;31))*( (((c-15709)&gt;&gt;31)+1)*(-(a&gt;&gt;31|1)*31416+2*a)  ), %_SIN%) / 10000"
    set "_SIN="
    

for /l %%# in () do (
    
    for /f "tokens=1-3 delims=:" %%a in ("!time:~0,-3!") do (
        set "th=%%a"
        set "tm=%%b"
        set "ts=%%c"
        if "!tm:~0,1!" equ "0" set "tm=!tm:~1!"
        if "!ts:~0,1!" equ "0" set "ts=!ts:~1!"
    )
    echo %esc%[25;22H!th!:!tm!:!ts!
    
    for /l %%c in (0,15,360) do ( set /a "cx=20 * !cos(x):x=%%c! + 25", "cy=20 * !sin(x):x=%%c! + 25"
        %plot_HSL_RGB% !cx! !cy! 1 10000 10000 Û
    )
    set /a "harc=270 + (30 * th)"
    for /l %%c in (270,15,!harc!) do ( set /a "cx=20 * !cos(x):x=%%c! + 25", "cy=20 * !sin(x):x=%%c! + 25"
        %plot_HSL_RGB% !cx! !cy! 330 10000 4000 Û
    )
    
    for /l %%c in (0,15,360) do ( set /a "cx=17 * !cos(x):x=%%c! + 25", "cy=17 * !sin(x):x=%%c! + 25"
        %plot_HSL_RGB% !cx! !cy! 1 10000 10000 Û
    )

    set /a "marc=270 + tm * 6"
    for /l %%c in (270,15,!marc!) do ( set /a "cx=17 * !cos(x):x=%%c! + 25", "cy=17 * !sin(x):x=%%c! + 25"
        %plot_HSL_RGB% !cx! !cy! 240 10000 4000 Û
    )
    
    for /l %%c in (0,15,360) do ( set /a "cx=14 * !cos(x):x=%%c! + 25", "cy=14 * !sin(x):x=%%c! + 25"
        %plot_HSL_RGB% !cx! !cy! 1 10000 10000 Û
    )
    
    set /a "sarc=270 + ts * 6"
    for /l %%c in (270,15,!sarc!) do ( set /a "cx=14 * !cos(x):x=%%c! + 25", "cy=14 * !sin(x):x=%%c! + 25"
        %plot_HSL_RGB% !cx! !cy! 110 10000 4000 Û
    )
    
    
    &lt;nul set /p "=%esc%[2J%esc%[?25l!screen!"
    set "screen="
)
pause&gt;nul







:macros
set ^"LF=^

^" Above empty line is required - do not remove
set ^"\n=^^^%LF%%LF%^%LF%%LF%^^"
for /F %%a in ('echo prompt $E^| cmd') do set "ESC=%%a"

rem %RGBplot% x y 0-255 0-255 0-255 CHAR
set RBGplot=for %%# in (1 2) do if %%#==2 ( for /f "tokens=1-6" %%1 in ("^!args^!") do (%\n%
  set "screen=^!screen^!^!esc^![%%2;%%1H^!esc^![38;2;%%3;%%4;%%5m%%6^!esc^![0m"%\n%
)) else set args=

REM plot_HSL_RGB x y 0-360 0-10000 0-10000
set plot_HSL_RGB=for %%# in (1 2) do if %%#==2 ( for /f "tokens=1-6" %%1 in ("^!args^!") do (%\n%
    set /a "H=%%~3", "S=%%~4", "L=%%~5"%\n%
    if %%3 geq 360 set /a "H=360"%\n%
    if %%3 leq 0   set /a "H=0"%\n%
    set /a "va=2*L-10000"%\n%
    for /f "tokens=1" %%a in ("^!va^!") do if %%a lss 0 set /a "va=-va"%\n%
    set /a "C=(10000-va)*S/10000"%\n%
    set /a "h1=H*10000/60"%\n%
    set /a "mm=(h1 %% 20000) - 10000"%\n%
    for /f "tokens=1" %%a in ("^!mm^!")  do if %%a lss 0 set /a "mm=-mm"%\n%
    set /a "X=C *(10000 - mm)/10000"%\n%
    set /a "m=L - C/2"%\n%
    for /f "tokens=1" %%a in ("^!H^!") do (%\n%
        if %%a lss 60  ( set /a "R=C+m", "G=X+m", "B=0+m" ) else (%\n%
        if %%a lss 120 ( set /a "R=X+m", "G=C+m", "B=0+m" ) else (%\n%
        if %%a lss 180 ( set /a "R=0+m", "G=C+m", "B=X+m" ) else (%\n%
        if %%a lss 240 ( set /a "R=0+m", "G=X+m", "B=C+m" ) else (%\n%
        if %%a lss 300 ( set /a "R=X+m", "G=0+m", "B=C+m" ) else (%\n%
        if %%a lss 360 ( set /a "R=C+m", "G=0+m", "B=X+m" ))))))%\n%
    )%\n%
    set /a "R=R*255/10000", "G=G*255/10000", "B=B*255/10000"%\n%
    for /f "tokens=1-3" %%a in ("^!R^! ^!G^! ^!B^!") do ^!RBGplot^! %%1 %%2 %%a %%b %%c %%6%\n%
)) else set args=
goto :eof</code></pre></div>

<div class="codebox"><p>Code: <a href="#" onclick="selectCode(this); return false;">Select all</a></p><pre><code>@echo off &amp; setlocal EnableDelayedExpansion
mode 49,49

call :macros

	set /a "PI=(35500000/113+5)/10, PI_div_2=(35500000/113/2+5)/10, PIx2=2*PI, PI32=PI+PI_div_2"
    set "_SIN=a-a*a/1920*a/312500+a*a/1920*a/15625*a/15625*a/2560000-a*a/1875*a/15360*a/15625*a/15625*a/16000*a/44800000"
    set "SIN(x)=(a=(x * 31416 / 180)%%62832, c=(a&gt;&gt;31|1)*a, a-=(((c-47125)&gt;&gt;31)+1)*((a&gt;&gt;31|1)*62832)  +  (-((c-47125)&gt;&gt;31))*( (((c-15709)&gt;&gt;31)+1)*(-(a&gt;&gt;31|1)*31416+2*a)  ), %_SIN%) / 10000"
    set "COS(x)=(a=(15708 - x * 31416 / 180)%%62832, c=(a&gt;&gt;31|1)*a, a-=(((c-47125)&gt;&gt;31)+1)*((a&gt;&gt;31|1)*62832)  +  (-((c-47125)&gt;&gt;31))*( (((c-15709)&gt;&gt;31)+1)*(-(a&gt;&gt;31|1)*31416+2*a)  ), %_SIN%) / 10000"
    set "_SIN="
	

for /l %%# in () do (
	
	
	%= Capture time elements =%
	for /f "tokens=1-3 delims=:" %%a in ("!time:~0,-3!") do (
		set "th=%%a"
		set "tm=%%b"
		set "ts=%%c" 
		if "!tm:~0,1!" equ "0" set "tm=!tm:~1!"
		if "!ts:~0,1!" equ "0" set "ts=!ts:~1!"
	)
	echo %esc%[25;22H!th!:!tm!:!ts!
	
	
	%= Clock diameter =%
	for /l %%a in (1,8,360) do ( set /a "cx=20 * !cos(x):x=%%a! + 25", "cy=20 * !sin(x):x=%%a! + 25"
		set /a "H=%%a %% 360"
		%plot_HSL_RGB% !cx! !cy! !H! 10000 5000 ÛÛ
	)
	
	%= HOUR LINE =%
	set /a "harc=270 + (30 * th)"
	set /a "cx=20 * !cos(x):x=harc! + 25", "cy=20 * !sin(x):x=harc! + 25"
	%line% 25 25 !cx! !cy! Û 9
	
	%= MINUTE LINE =%
	set /a "marc=270 + tm * 6"
	set /a "cx=17 * !cos(x):x=marc! + 25", "cy=17 * !sin(x):x=marc! + 25"
	%line% 25 25 !cx! !cy! Û 10

	%= SECOND LINE=%
	set /a "sarc=270 + ts * 6"
	set /a "cx=14 * !cos(x):x=sarc! + 25", "cy=14 * !sin(x):x=sarc! + 25"
	%line% 25 25 !cx! !cy! Û 6
	
	&lt;nul set /p "=%esc%[2J%esc%[?25l!screen!"
	set "screen="
)
pause&gt;nul







:macros
set ^"LF=^

^" Above empty line is required - do not remove
set ^"\n=^^^%LF%%LF%^%LF%%LF%^^"
for /F %%a in ('echo prompt $E^| cmd') do set "ESC=%%a"

rem %RGBplot% x y 0-255 0-255 0-255 CHAR
set RBGplot=for %%# in (1 2) do if %%#==2 ( for /f "tokens=1-6" %%1 in ("^!args^!") do (%\n%
  set "screen=^!screen^!^!esc^![%%2;%%1H^!esc^![38;2;%%3;%%4;%%5m%%6^!esc^![0m"%\n%
)) else set args=

REM plot_HSL_RGB x y 0-360 0-10000 0-10000
set plot_HSL_RGB=for %%# in (1 2) do if %%#==2 ( for /f "tokens=1-6" %%1 in ("^!args^!") do (%\n%
	set /a "H=%%~3", "S=%%~4", "L=%%~5"%\n%
	if %%3 geq 360 set /a "H=360"%\n%
	if %%3 leq 0   set /a "H=0"%\n%
	set /a "va=2*L-10000"%\n%
	for /f "tokens=1" %%a in ("^!va^!") do if %%a lss 0 set /a "va=-va"%\n%
	set /a "C=(10000-va)*S/10000"%\n%
	set /a "h1=H*10000/60"%\n%
	set /a "mm=(h1 %% 20000) - 10000"%\n%
	for /f "tokens=1" %%a in ("^!mm^!")  do if %%a lss 0 set /a "mm=-mm"%\n%
	set /a "X=C *(10000 - mm)/10000"%\n%
	set /a "m=L - C/2"%\n%
	for /f "tokens=1" %%a in ("^!H^!") do (%\n%
		if %%a lss 60  ( set /a "R=C+m", "G=X+m", "B=0+m" ) else (%\n%
		if %%a lss 120 ( set /a "R=X+m", "G=C+m", "B=0+m" ) else (%\n%
		if %%a lss 180 ( set /a "R=0+m", "G=C+m", "B=X+m" ) else (%\n%
		if %%a lss 240 ( set /a "R=0+m", "G=X+m", "B=C+m" ) else (%\n%
		if %%a lss 300 ( set /a "R=X+m", "G=0+m", "B=C+m" ) else (%\n%
		if %%a lss 360 ( set /a "R=C+m", "G=0+m", "B=X+m" ))))))%\n%
	)%\n%
	set /a "R=R*255/10000", "G=G*255/10000", "B=B*255/10000"%\n%
	for /f "tokens=1-3" %%a in ("^!R^! ^!G^! ^!B^!") do ^!RBGplot^! %%1 %%2 %%a %%b %%c %%6%\n%
)) else set args=

rem %plot% x y 0-255 0-255 CHAR
set plot=for %%# in (1 2) do if %%#==2 ( for /f "tokens=1-5" %%1 in ("^!args^!") do (%\n%
  set "screen=^!screen^!!esc![%%2;%%1H!esc![38;5;%%3m!esc![48;5;%%4m%%~5!esc![0m"%\n%
)) else set args=

rem LINE x0 y0 x1 y1 COLOR CHAR
set line=for %%# in (1 2) do if %%#==2 ( for /f "tokens=1-6" %%1 in ("^!args^!") do (%\n%
	if "%%~6" equ "" ( set "hue=30" ) else ( set "hue=%%~6")%\n%
	set /a "xa=%%~1", "ya=%%~2", "xb=%%~3", "yb=%%~4", "dx=%%~3 - %%~1", "dy=%%~4 - %%~2"%\n%
	for /f "tokens=1-2" %%6 in ("^!dx^! ^!dy^!") do (%\n%
		if %%~7 lss 0 ( set /a "dy=-%%~7", "stepy=-1" ) else ( set "stepy=1" )%\n%
		if %%~6 lss 0 ( set /a "dx=-%%~6", "stepx=-1" ) else ( set "stepx=1" )%\n%
		set /a "dx&lt;&lt;=1", "dy&lt;&lt;=1"%\n%
	)%\n%
	for /f "tokens=1-9" %%a in ("^!dx^! ^!dy^! ^!xa^! ^!xb^! ^!ya^! ^!yb^! ^!stepx^! ^!stepy^! ^!hue^!") do (%\n%
		if %%~a gtr %%~b (%\n%
			set /a "fraction=%%~b - (%%~a &gt;&gt; 1)"%\n%
			for /l %%x in (%%~c,%%~g,%%~d) do (%\n%
				for /f "tokens=1" %%6 in ("^!fraction^!") do if %%~6 geq 0 set /a "ya+=%%~h", "fraction-=%%~a"%\n%
				set /a "fraction+=%%~b"%\n%
				for /f "tokens=1" %%6 in ("^!ya^!") do (%\n%
					if 0 leq %%x if %%x lss 199 if 0 leq %%~6 if %%~6 lss 199 ^!plot^! %%x %%~6 %%i 0 %%~5%\n%
				)%\n%
			)%\n%
		) else (%\n%
			set /a "fraction=%%~a - (%%~b &gt;&gt; 1)"%\n%
			for /l %%y in (%%~e,%%~h,%%~f) do (%\n%
				for /f "tokens=1" %%6 in ("^!fraction^!") do if %%~6 geq 0 set /a "xa+=%%~g", "fraction-=%%~b"%\n%
				set /a "fraction+=%%~a"%\n%
				for /f "tokens=1" %%6 in ("^!xa^!") do (%\n%
					if 0 leq %%~6 if %%~6 lss 199 if 0 leq %%y if %%y lss 199 ^!plot^! %%~6 %%y %%i 0 %%~5%\n%
				)%\n%
			)%\n%
		)%\n%
	)%\n%
)) else set args=
goto :eof</code></pre></div></div>
			</div>
			<hr />
					<div class="post">
				<h3>Re: Visualize a clock</h3>
				<div class="date">Posted: <strong>06 May 2018 22:41</strong></div>
				<div class="author">by <strong>Aacini</strong></div>
				<div class="content">This remembers me <a href="viewtopicc14f.html?f=3&amp;t=5028&amp;p=45842#p45842" class="postlink">my analog clock</a> written in pure Batch:<br>
<br>
<img src="../../i.imgur.com/DrnDEZR.png" class="postimage" alt="Image"><br>
<br>
The output looks better if a small square font is used, like the bitmap raster font @ size 8 X 8.<br>
<br>
Antonio</div>
			</div>
			<hr />
					<div class="post">
				<h3>Re: Visualize a clock</h3>
				<div class="date">Posted: <strong>08 May 2018 00:12</strong></div>
				<div class="author">by <strong>Kvc</strong></div>
				<div class="content">Wow! Great work... I can't imagine achieving all this beautiful output on Console without a plugin. Well Done! And, Aacini - your work really touched me again. <img class="smilies" src="images/smilies/icon_smile.gif" width="15" height="15" alt=":)" title="Smile"><br>
<br>
Here's screenshot of <a href="http://www.thebateam.org/2017/03/how-to-make-analog-clock-with-notepad.html" class="postlink">the analog clock project</a> that I've tried in past. But, its code uses few plugins/functions/external commands (some of them i've created, and most are creation of great programmers).<br>
<br>
<img src="../../3.bp.blogspot.com/-uJlbRPX4GPc/WMHxXJtQHLI/AAAAAAAAB-o/UPPTtpLulP4pvTJqKaE0aQmSo1mu_vn2gCLcB/s1600/2017-03-10%20(1).png" class="postimage" alt="Image"></div>
			</div>
			<hr />
					<div class="post">
				<h3>Re: Visualize a clock</h3>
				<div class="date">Posted: <strong>08 May 2018 15:00</strong></div>
				<div class="author">by <strong>misol101</strong></div>
				<div class="content"><img src="https://ucc230fb83af774c79fa8ec2906d.dl.dropboxusercontent.com/cd/0/inline/ATYXaKDgq3KTp9lDSpIjr3MjdoM4tuO8wAWIh16_CrU1KA6L2xA4bdkou8tbtNajf7hbjPVU_RzxozkFc7XJNOZJHGL16zsqzArULiDD_mOGkldfoISZ53mWt1TCuhfexQDLmYutTKryGBtx9iBqSQUDvj277-PBRbxSdZckqi-ovf8WR0zBc9vTcceBYxhyHgk/file" class="postimage" alt="Image"><br>
<br>
I did a quick mashup of a matrix effect with a digital clock to contribute something: <a href="http://www.mediafire.com/file/9e1akejye6se6df/matrix-clock.zip" class="postlink">http://www.mediafire.com/file/9e1akejye ... -clock.zip</a><br>
<br>
ENTER to toggle rotation of digits, SPACE to change colors<br>
<br>
Like KVC's clock, it uses plugins.<br>
<br>
<br>
EDIT: the archive was updated with another rotating blue clock, plus the ultimate uselessness: the fire clock <img class="smilies" src="images/smilies/icon_smile.gif" width="15" height="15" alt=":)" title="Smile"></div>
			</div>
			<hr />
					<div class="post">
				<h3>Re: Visualize a clock</h3>
				<div class="date">Posted: <strong>09 May 2018 06:57</strong></div>
				<div class="author">by <strong>Compo</strong></div>
				<div class="content">I love working with batch files, but I've got to be honest, this task is completely unnecessary and wasteful.<br>
<br>
At all times whilst using my PC, there's a direct click to open a clock which gives me everything I need!<br>
<img src="../../i.imgur.com/m9cebEG.gif" class="postimage" alt="Image"></div>
			</div>
			<hr />
					<div class="post">
				<h3>Re: Visualize a clock</h3>
				<div class="date">Posted: <strong>09 May 2018 08:13</strong></div>
				<div class="author">by <strong>misol101</strong></div>
				<div class="content">Compo: that's a clear case of cheating  <img class="smilies" src="images/smilies/icon_biggrin.gif" width="15" height="15" alt=":D" title="Very Happy"></div>
			</div>
			<hr />
					<div class="post">
				<h3>Re: Visualize a clock</h3>
				<div class="date">Posted: <strong>09 May 2018 09:20</strong></div>
				<div class="author">by <strong>IcarusLives</strong></div>
				<div class="content"><blockquote><div><cite><a href="memberlist1ec2.html?mode=viewprofile&amp;u=5449">Compo</a> wrote: <a href="viewtopic02ae.html?p=56733#p56733" data-post-id="56733" onclick="if(document.getElementById(hash.substr(1)))href=hash">↑</a><div class="responsive-hide">09 May 2018 06:57</div></cite>
I love working with batch files, but I've got to be honest, this task is completely unnecessary and wasteful.<br>
<br>
At all times whilst using my PC, there's a direct click to open a clock which gives me everything I need!<br>
<img src="../../i.imgur.com/m9cebEG.gif" class="postimage" alt="Image">
</div></blockquote>

By no means am I trying to reproduce a BETTER clock in pure batch. This is purely to experiment with code and shapes (at least from my stand point)</div>
			</div>
			<hr />
			</div>

	<div id="page-footer" class="page-footer">
		<div class="page-number">All times are <span title="UTC-6">UTC-06:00</span><br />Page <strong>1</strong> of <strong>1</strong></div>
		<div class="copyright">Powered by phpBB&reg; Forum Software &copy; phpBB Limited<br />https://www.phpbb.com/</div>
	</div>
</div>

</body>

<!-- Mirrored from www.dostips.com/forum/viewtopic.php?f=3&t=8546&view=print by HTTrack Website Copier/3.x [XR&CO'2014], Fri, 19 Oct 2018 07:21:48 GMT -->
</html>
